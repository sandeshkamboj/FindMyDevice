plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'kotlin-parcelize'
    id 'org.jlleitschuh.gradle.ktlint'
}

android {
    namespace = "com.save.me"
    compileSdk = 34

    defaultConfig {
        applicationId = "com.save.me"
        minSdk = 29
        targetSdk = 34
        versionCode = 1
        versionName = "1.0"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    // --- BEGIN: Signing config using local.properties or environment variables ---
    signingConfigs {
        release {
            def props = new Properties()
            def localPropsFile = rootProject.file("local.properties")
            if (localPropsFile.exists()) {
                localPropsFile.withInputStream { props.load(it) }
            }
            def getProp = { name ->
                return System.getenv(name) ?: props[name] ?: ''
            }
            storeFile file(getProp('RELEASE_STORE_FILE'))
            storePassword getProp('RELEASE_STORE_PASSWORD')
            keyAlias getProp('RELEASE_KEY_ALIAS')
            keyPassword getProp('RELEASE_KEY_PASSWORD')
        }
    }
    // --- END: Signing config ---

    buildTypes {
        debug {
            signingConfig signingConfigs.release // <-- Makes debug use the release keystore
        }
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debugMini {
            initWith(debug)
            debuggable = true
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['debug']
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xcontext-receivers"]
    }
    buildFeatures {
        buildConfig = true
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.11'
    }
    packagingOptions {
        resources {
            excludes += ['/META-INF/{AL2.0,LGPL2.1}']
        }
    }
    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }
}

ktlint {
    android = true
    ignoreFailures = false
    reporters {
        reporter "plain"
        reporter "checkstyle"
        reporter "json"
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.fragment:fragment-ktx:1.6.2' // Added to fix release build error (registerForActivityResult API)
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.activity:activity-compose:1.9.0'
    implementation platform('androidx.compose:compose-bom:2024.05.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.animation:animation'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2024.05.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta02'
    // === CORRECT COROUTINES DEPENDENCIES ===
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1")
    // =======================================
    implementation 'io.coil-kt:coil-compose:2.6.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.navigation:navigation-compose:2.7.7'
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'androidx.room:room-ktx:2.6.1'
    implementation 'androidx.room:room-paging:2.6.1'
    kapt 'androidx.room:room-compiler:2.6.1'
    implementation 'androidx.paging:paging-runtime:3.3.0'
    implementation 'androidx.paging:paging-compose:3.3.0'
    implementation 'androidx.work:work-runtime-ktx:2.9.0'
    implementation 'com.github.Pengrad:java-telegram-bot-api:6.9.1'
    implementation 'androidx.compose.material:material-icons-extended:1.6.7'
    implementation 'androidx.compose.material3:material3-adaptive-navigation-suite:1.3.2'

    // FCM dependencies
    implementation platform('com.google.firebase:firebase-bom:32.7.3')
    implementation 'com.google.firebase:firebase-messaging-ktx'

    // Play Services Location, if you use location APIs elsewhere
    implementation 'com.google.android.gms:play-services-location:21.1.0'
}

apply plugin: 'com.google.gms.google-services'